# gnuplot TEMPLATE for Table3D

# Copyright (C) 2011-2015 SubaruDieselCrew
#
# This file is part of ScoobyRom.
#
# ScoobyRom is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# ScoobyRom is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with ScoobyRom.  If not, see <http://www.gnu.org/licenses/>.


# Note: page numbers in comments refer to official gnuplot documentation PDF from www.gnuplot.info
# page numbers depend on version obviously, may be outdated
# current version: gnuplot 5.0

# command "test" is useful to check terminal specific colors, linetypes, capabilities...
# test

# -------- data specific commands generated by the app, before loading this template file:
# set xlabel "Engine Speed\n{/mono [1/min]}" ...
# set ylabel ...
# set title ...
# call 'this_template_file.plt' 'pathToBinaryDataFile'
# --------

set macros

# first argument ARG1 to this script must be path of binary data file
dataFile = ARG1
#print dataFile
# variable set by app: windowtitle = "..."

# interactive terminals tested on Windows: qt, wxt, windows
# override terminal if needed, better use default (i.e. OS environment variable GNUTERM)
# set term qt

# font and size
# Gnuplot-defined variable GPVAL_TERM has currently active terminal i.e. "qt"
set term GPVAL_TERM font "sans,12

set term GPVAL_TERM title windowtitle

# to allow inline text formatting
set termoption enhanced

# HSV color interpolation: min, max as HSV
set palette model HSV defined (0 0.66666667 0.4 1, 1 0 0.6 1)

# default: front = in front of actual plot
set colorbox back

# horizontal, wide and near bottom if wanted
# set colorbox horiz user origin .1,.1 size .8,.04

# define style id 100 for surface lines (polygon frame), linetype -1 always means solid black line
set style line 100 linetype -1 linecolor "black" linewidth 0.5

set pm3d depthorder hidden3d

# p.150: border: draw bounding lines, use own style
set pm3d border linestyle 100

# add contour lines; displayed on base, surface or both
set contour surface
# changing this needs replot:
# bspline may not be accurate but is always smooth
set cntrparam bspline
set cntrparam levels auto 10

# one color only and no contour legend
#set cntrlabel onecolor


# p.182: "By default splot draws the xy plane completely below the plotted data. The oï¬€set between the lowest ztic and the xy plane can be changed by set xyplane."
# p.178: xyplane; default: set xyplane relative 0.5
# set xyplane at 20
# should work for most/all 3d plots, little space so that lowest z-label does not collide with xy-label
set xyplane relative 0.05

# clipping effect or to adjust lowest value to base surface etc.
# set zrange [20:62]
# autoscale:
# set zrange [*:*]


# show default grid
set grid

# viewing angle
# p.170; set view <rot_x>{,{<rot_z>}{,{<scale>}{,<scale_z>}}}
# defaults to: set view 60, 30, 1, 1
# <0 not allowed: rot_x [0:180], rot_z [0:360]
viewDefault = "30,330"
set view viewDefault

# p.39; key bindings
# press "home", sometimes labeled "pos1", key to reset viewing angle
bind Home "set view viewDefault; refresh"

# without specifying title, "<filename> binary" would appear above contour legend!
# empty title avoids this

# p.83: "The default binary format is a float."
# app generates a binary data file on each plot action, basically a temporary file containing coordinates
# mark as "volatile" so a single temporary data file can be shared, replot would re-read and could show wrong data

splot dataFile binary volatile title "" with pm3d

#set label 1 "Annotation Label" at screen 0.01,0.95 front left textcolor rgb "blue"


# -------- SVG output (commands sent via standard input from app)
# could also do PDF, LaTeX, PNG etc.
## remember current terminal
# set terminal push
# set terminal svg size 1200,800 dynamic enhanced font "sans,16"
# set output "SomeFileName.svg"
# refresh
## switch back to previous terminal
# set terminal pop
# --------
